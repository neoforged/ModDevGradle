plugins {
    id 'net.neoforged.moddev'
}

evaluationDependsOn(":coremod") // Because of the sourceset reference

dependencies {
    implementation project(":coremod")

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    jarJar(project(":coremod"))

    jarJar("org.commonmark:commonmark") {
        version {
            strictly '[0.1, 1.0)'
            prefer '0.21.0'
        }
    }
}

test {
    useJUnitPlatform()
}

neoForge {
    version = project.neoforge_version

    runs {
        data {
            data()
        }
    }

    mods {
        jijtest {
            sourceSet sourceSets.main
        }
        coremod {
            sourceSet project(":coremod").sourceSets.main
        }
    }

    unitTest {
        enable()
        testedMod = mods.jijtest
        loadedMods = [mods.coremod]
    }
}
