plugins {
    id 'net.neoforged.moddev'
}

evaluationDependsOn(":subproject") // Because of the sourceset reference

sourceSets {
    api
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "net.neoforged:testframework:${project.neoforge_version}"

    implementation(accessTransformers(interfaceInjectionData(project(":subproject"))))
    implementation sourceSets.api.output
}

test {
    useJUnitPlatform()
}

neoForge {
    version = project.neoforge_version
    addModdingDependenciesTo(sourceSets.api)

    interfaceInjectionData = files("interfaces.json")

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.INFO
        }
        client {
            client()
        }
        client2 {
            client()
            programArguments.addAll('--username', 'Dev2')
        }
        data {
            data()
        }
        server {
            server()
        }
        apitest {
            client()
            sourceSet = sourceSets.api
        }
    }

    mods {
        testproject {
            sourceSet sourceSets.main
            sourceSet sourceSets.api
            sourceSet project(":subproject").sourceSets.main
        }
    }

    parchment {
        minecraftVersion = "1.21"
        mappingsVersion = "2024.06.23"
    }

    unitTest {
        enable()
        testedMod = mods.testproject
    }
}

neoFormRuntime {
    useEclipseCompiler = true
    // enableCache = false
    // verbose = true

    additionalResults.put('vanillaDeobfuscated', project.file('build/vanilla.jar'))
}
