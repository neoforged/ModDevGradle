plugins {
    id 'net.neoforged.moddev'
}

repositories {
    mavenLocal()
}

sourceSets {
    api
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "net.neoforged:testframework:${project.neoforge_version}"

    implementation project(":subproject")
    implementation sourceSets.api.output
}

test {
    useJUnitPlatform()
}

neoForge {
    version = project.neoforge_version
    addModdingDependenciesTo(sourceSets.api)

    interfaceInjectionData = files("interfaces.json")

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.INFO
        }
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
        apitest {
            client()
            sourceSet = sourceSets.api
        }
    }

    mods {
        testproject {
            sourceSet sourceSets.main
            sourceSet sourceSets.api
            dependency project(":subproject")
        }
    }

    neoFormRuntime {
        useEclipseCompiler = true
        // enableCache = false
        // verbose = true
    }

    parchment {
        minecraftVersion = "1.21"
        mappingsVersion = "2024.06.23"
    }

    unitTest {
        enable()
        testedMod = mods.testproject
    }
}
