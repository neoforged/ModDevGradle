plugins {
    id 'net.neoforged.moddev.legacyforge'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name 'cursemaven'
        url 'https://cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    modCompileOnly('mezz.jei:jei-1.20.1-forge:15.17.0.76')
    modRuntimeOnly('curse.maven:mekanism-268560:5662583')
    modImplementation('curse.maven:applied-energistics-2-223794:5641282')
    jarJar('curse.maven:applied-energistics-2-223794:5641282')
    jarJar(project(':nonmc'))
}

legacyForge {
    version = '1.20.1-47.3.12'
    runs {
        client {
            client()
        }
        data {
            data()
        }
    }
    mods {
        myMod {
            sourceSet(sourceSets.main)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url rootProject.file('repo')
        }
    }
}
